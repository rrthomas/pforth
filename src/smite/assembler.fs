\ SMite assembler for pForth
\ Reuben Thomas   started 1/95

\ FIXME: Add an IP which allows advantage to be taken of packing opcodes
\ into the current word after an instruction with an operand is assembled

CR .( SMite assembler )


VOCABULARY ASSEMBLER  ALSO ASSEMBLER DEFINITIONS
: BITS   S" ADDRESS-UNIT-BITS" ENVIRONMENT?
   INVERT ABORT" ADDRESS-UNIT-BITS query not supported" ;
BITS  CONSTANT BITS/

FORTH DEFINITIONS
: CODE   BL WORD HEADER  ALSO ASSEMBLER ;
: END-CODE   ALIGN  PREVIOUS ;
ASSEMBLER DEFINITIONS

: INLINE   ( char -- )   LAST >INFO 2 + C! ;

: OPLESS   CREATE C,  DOES> C@ C, ;
: 0OPS   SWAP 1+ SWAP DO  I OPLESS  LOOP ;

\ FIXME: Use different prefix, not B!
$87 $80 0OPS  BNOP    BPOP    BDUP    BSWAP   BLT     BEQ     BULT    BADD
$8F $88 0OPS  BNEGATE BMUL    BUDIVMOD BDIVMOD BINVERT BAND   BOR     BXOR
$97 $90 0OPS  BLSHIFT BRSHIFT BLOAD   BSTORE  BLOADB  BSTOREB BBRANCH BBRANCHZ
$9C $98 0OPS  BHALT   BCALL_NATIVE BEXTRA BPUSH_WORD_SIZE BPUSH_NATIVE_POINTER_SIZE
$A0 $9D 0OPS  BPUSH_FRAME_DEPTH BSTORE_FRAME_DEPTH BPUSH_PC BPUSH_MEMORY
$A5 $A1 0OPS  BPUSH_F0 BSTORE_F0 BPUSH_FRAME BPOP_FRAME BLOAD_FRAME_VALUE
$AA $A6 0OPS  BLOAD_OUTER_F0 BLOAD_OUTER_DEPTH BFRAME_DUP BFRAME_SWAP BCALL_FRAME

6 CONSTANT LITERAL-CHUNK-BIT
1 LITERAL-CHUNK-BIT LSHIFT  1-  CONSTANT LITERAL-CHUNK-MASK
$40 CONSTANT LITERAL-CONTINUATION

PREVIOUS DEFINITIONS
